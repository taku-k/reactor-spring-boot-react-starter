buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'net.saliman.cobertura' version '2.3.1'
    id 'com.github.kt3k.coveralls' version '2.7.1'
}

cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report

apply plugin: 'java'
apply plugin: 'spring-boot'

jar {
    baseName = 'reactor-spring-boot-react-server'
    version = '0.0.1-SNAPSHOT'
    from('../ui/build') {
        into 'static'
    }
}

task uiBuild(dependsOn: ':ui:assetsBuild')
task serverBuild(dependsOn: assemble) {
    mustRunAfter uiBuild
}
task allAssemble(dependsOn: [serverBuild, uiBuild])

test {
    testLogging.showStandardStreams = true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.projectlombok:lombok:1.16.10'
    compile 'com.google.guava:guava:20.0'
    compile 'io.reactivex:rxjava:1.2.2'
    compile 'io.reactivex.rxjava2:rxjava:2.0.2'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.1'
    compile 'org.yaml:snakeyaml:1.17'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'commons-io:commons-io:2.5'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.assertj:assertj-core:3.2.0'
}
