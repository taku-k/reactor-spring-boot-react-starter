buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'net.saliman.cobertura' version '2.3.1'
    id 'com.github.kt3k.coveralls' version '2.7.1'
    id 'com.github.sherter.google-java-format' version '0.6'
}

cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report

apply plugin: 'java'
apply plugin: 'org.springframework.boot'

jar {
    baseName = 'rx-spring-boot-react-starter'
    version = '0.1.0'
    from('../ui/build') {
        into 'static'
    }
}

task uiBuild(dependsOn: ':ui:assetsBuild')
task serverBuild(dependsOn: assemble) {
    mustRunAfter uiBuild
}
task allAssemble(dependsOn: [clean, serverBuild, uiBuild])

test {
    testLogging.showStandardStreams = true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.projectlombok:lombok:1.16.10'
    compile 'com.google.guava:guava:20.0'
    compile 'io.reactivex.rxjava2:rxjava:2.0.9'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.1'
    compile 'org.yaml:snakeyaml:1.17'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'commons-io:commons-io:2.5'

    // For OpenTracing
    compile 'io.opentracing:opentracing-api:0.21.0'
    compile 'com.uber.jaeger:jaeger-core:0.18.0'
    compile 'io.opentracing.contrib:opentracing-spring-web:0.0.3'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.assertj:assertj-core:3.2.0'
}
